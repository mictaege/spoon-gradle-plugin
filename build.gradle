group 'com.github.mictaege'
version '2020.2-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

wrapper {
	gradleVersion = "5.4.1"
	distributionType = Wrapper.DistributionType.ALL
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile gradleApi()
    compile 'org.codehaus.groovy:groovy-all:2.5.8'
    compile 'com.google.guava:guava:23.0'
    compile 'com.github.javaparser:javaparser-core:3.5.4'
    compile 'com.github.javaparser:java-symbol-solver-core:0.6.2'
    compile('fr.inria.gforge.spoon:spoon-core:7.5.0') {
        exclude group: "org.eclipse.jdt", module : "org.eclipse.jdt.core"
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") } //sign only when upload archives
    sign configurations.archives
}


//don't refer to username and pwd directly so it could be executed in another environment with another _gradle.properties_
String publishUsername = hasProperty("ossrhUsername") ? property("ossrhUsername") : ""
String publishPassword = hasProperty("ossrhPassword") ? property("ossrhPassword") : ""

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: publishUsername, password: publishPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: publishUsername, password: publishPassword)
            }

            pom.project {
                name 'spoon-gradle-plugin'
                packaging 'jar'
                description 'The spoon-gradle-plugin is a gradle plugin for the Java analysis and transformation framework Spoon.'
                url 'https://github.com/mictaege/spoon-gradle-plugin'
                scm {
                    connection 'https://github.com/mictaege/spoon-gradle-plugin.git'
                    url 'https://github.com/mictaege/spoon-gradle-plugin'
                }
                licenses {
                    license {
                        name 'Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id 'mictaege'
                        name 'Michael Taege'
                    }
                }
            }
        }
    }
}
